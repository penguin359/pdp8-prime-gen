mul=7405
div=7407
*0
	jmp	init

*10
index,	0

*200
init,
	cla
	tad	(hello)
	jms	prntln
	tad	(name)
	jms	prntln
	decimal
	tad	(123)
	jms	prntnum
	tad	(0)
	jms	prntnum
	tad	(4095)
	jms	prntnum
	tad	(7)
	jms	prntnum
	tad	(12)
	jms	prntnum
	octal
	tad	(hello)
	jms	prntln
	jmp	start
hello,	"H; "e; "l; "l; "o; ",; " ; "W; "o; "r; "l; "d; "!; 15; 12; 0
name,	"W; "h; "a; "t; " ; "i; "s; " ; "y; "o; "u; "r; " ; "n; "a; "m; "e; "?; 15; 12; 0
bgnstr,	"B; "E; "G; "I; "N; 15; 12; 0
endstr,	"E; "N; "D; 15; 12; 0
PAGE
col,	0
row,	0
colsav,	0
rowsav,	0
num,	0
multi,	0
count,	0
start,
	tad	(bgnstr)
	jms	prntln
	cla
	tad	(0001)
	dca	col
	tad	(primes-1)
	dca	index
	tad	[7774]
	dca I	index
nxtld,
	cla
	tad	[7777]
	dca I	index
	tad	index		/ Load the row index and
	cia			/ subtract it from the
	tad	(primes+526-1)	/ address of last bit-field for primes
	sza
	jmp	nxtld

	tad	(0001)
	dca	col
	tad	(primes)
	dca	row
nxtnum,
	cla
	tad I	row
	and	col
	sna
	jmp	noprint

	/ This is prime
	/ print it and mark multiples as non-prime
	cla
	tad	num
	jms	prntnum

	/ Save current row/col
	cla
	tad	col
	dca	colsav
	tad	row
	dca	rowsav

	tad	num
	dca	multi
rstrtmu,
	tad	num
	cia			/ two's complement AC
	dca	count

nxtcnt,
	/ Search through word space until multi wraps back to zero
	jms	incr
	isz	multi
	jmp	chkmlt
	jmp	final		/ Overflow back to zero while searching for multi
	/ Check if this number is a multiple
chkmlt,
	isz	count
	jmp	nxtcnt		/ Nope, skip to next number
	tad	col		/ Yes, mark it as non-prime
	cma
	and I	row
	dca I	row
	jmp	rstrtmu

final,
	/ Restore original row/col
	tad	colsav
	dca	col
	tad	rowsav
	dca	row

noprint,
	jms	incr
	isz	num
	jmp	nxtnum
	tad	(endstr)
	jms	prntln
	hlt

incr,
	0
	/ Load col into accumulator while leaving link clear
	cla cll
	tad	col
	ral
	snl
	jmp	incrcol
	ral
	isz	row
incrcol,
	dca	col
	jmp I	incr

PAGE
prntbuf, 0; 0; 0; 0; 15; 12; 0
prntnum,	0
	mql
	div; 12
	tad	("0)
	dca	prntbuf+3
	div; 12
	tad	("0)
	dca	prntbuf+2
	div; 12
	tad	("0)
	dca	prntbuf+1
	mqa
	tad	("0)
	dca	prntbuf+0
	tad	(prntbuf)
	jms	prntln
	jmp I	prntnum

prntln, 0
	tad	(-1)
	dca	index
pl1,
	tad I 	index	/ Load next character with auto-increment
	sna		/ Check for NUL
	jmp I	prntln	/ Return from subroutine
	tls		/ Clear flag and send byte: TCF + TPC
	tsf		/ Wait for byte to be sent
	jmp	.-1
	cla
	jmp	pl1

PAGE
primes,
$
